plugins {
    id 'org.springframework.boot' version '3.5.11'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'com.palantir.docker' version '0.36.0'
    id 'checkstyle'
}

checkstyle {
    configFile = 'config/checkstyle/checkstyle.xml' as File
    configProperties.suppressionsFile = file('config/checkstyle/suppressions.xml').absolutePath
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.21.1'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'jakarta.validation:jakarta.validation-api'

    implementation 'com.mysql:mysql-connector-j:8.4.0'
    implementation 'com.h2database:h2:2.4.240'
    implementation 'org.liquibase:liquibase-core:4.33.0'

    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'uk.org.webcompere:model-assert:1.1.0'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'com.h2database:h2:2.4.240'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

tasks.withType(JavaCompile).configureEach { options.encoding = "UTF-8" }
tasks.withType(JavaExec).configureEach { defaultCharacterEncoding = "UTF-8" }
tasks.withType(Javadoc).configureEach { options.encoding = "UTF-8" }
tasks.withType(Test).configureEach { defaultCharacterEncoding = "UTF-8" }

tasks.register('cleanLogs', Delete) { delete fileTree(dir: 'logs') }
tasks.named('clean') { dependsOn tasks.named('cleanLogs') }

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

jar {
    enabled = false
}

bootJar {
    enabled = true
    archiveBaseName = 'black-list'
    archiveVersion = '1.0.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Identify the artifact
            artifact bootJar  // This refers to the Spring Boot's bootJar task

            // Project coordinates
            groupId = 'lv.javaguru'
            artifactId = 'black-list'
            version = '1.0.0'

            // Add other metadata as needed
            pom {
                name = 'Black List'
                description = 'Blacklist application'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }    }

    // Configure the repository location (local Maven repository)
    repositories {
        mavenLocal()
    }
}

docker {
    dependsOn bootJar
    name "black-list:1.0.0"
    files 'build/libs/black-list-1.0.0.jar'
    files 'wait-for-it.sh'
    buildArgs(['JAR_FILE': "doc-generator-1.0.0.jar"])
}

tasks.named('dockerPrepare').configure {
    dependsOn bootJar // Explicit dependency on bootJar
}
